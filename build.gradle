plugins {
  id "java"
  id "com.github.spotbugs" version "4.7.9"
  id 'com.palantir.git-version' version '0.12.3'
}

apply plugin: 'com.github.spotbugs'
apply plugin: 'checkstyle'
apply plugin: 'maven-publish'

group 'io.setl'
if (
(versionDetails().branchName == null || (versionDetails().branchName ==~ /^(master)|(release.*)|(patch.*)$/))
    && versionDetails().isCleanTag
    && gitVersion() ==~ /^\d+(\.\d+)+(-.+)?$/
) {
  project.version = gitVersion()
} else {
  project.version = '100-SNAPSHOT'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.69'
  implementation 'javax.annotation:javax.annotation-api:1.3.2'
  implementation 'javax.json:javax.json-api:1.1.4'
  implementation 'io.setl:canonical-json:2.1'
  implementation 'javax.validation:validation-api:2.0.1.Final'
  implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.0'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
  implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.13.0'
  implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.0'
  implementation 'io.swagger.core.v3:swagger-annotations:2.1.11'
  implementation 'com.google.code.findbugs:annotations:3.0.1'

  testImplementation group: 'junit', name: 'junit', version: '4.12'
  testImplementation group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

  testRuntimeOnly group: 'junit', name: 'junit', version: '4.12'
}

checkstyle {
  toolVersion = "8.41.1"
  configFile = rootProject.file('config/checkstyle/checkstyle.xml')
}
checkstyleTest {
  enabled = false
}

spotbugs {
  ignoreFailures = true
  effort = 'max'
}
spotbugsMain {
  reports {
    html {
      enabled = true
    }
  }
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourceJar {
        classifier "sources"
      }
    }
  }
  repositories {
    maven {
      allowInsecureProtocol = true
      credentials {
        username = "$mavenUser"
        password = "$mavenPassword"
      }
      if (version.endsWith("SNAPSHOT")) {
        url 'http://si-nexus01.dev.setl.io:8081/repository/setl-snaps'
      } else {
        url 'http://si-nexus01.dev.setl.io:8081/repository/setl-libs'
      }
    }
  }
}

